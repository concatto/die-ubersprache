#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
Package = grammar
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = SLR
#RegularDefinitions
NUM: [0-9]
ID: [a-zA-Z_][a-zA-Z0-9_]*
HEX: 0x[0-9A-F]+
BIN: 0b[0-1]+
STR: \"[^\"]*\"
CHR: '.'
COMMENT: / /.*
M_COMMENT: /\*([^\*]|\*+[^\*/])*\*+/
#Tokens
INT_LIT : {NUM}+
FLOAT_LIT: {NUM}*\.{NUM}+
BIN_LIT : {BIN}
HEX_LIT : {HEX}
STRING_LIT : {STR}
CHAR_LIT: {CHR}

RB_OPEN : "("
RB_CLOSE : ")"
SB_OPEN : "["
SB_CLOSE : "]"
CB_OPEN : "{"
CB_CLOSE : "}"
INCREMENT: "++"
DECREMENT: "--"
PLUS : "+"
MINUS : "-"
DIV : "/"
MULT : "*"
COMMA : ","
MOD: "%" 
COLON : ":"
END : ";"
GT : "grösser als"
LT : "kleiner als"
GTE : "grösser oder gleich"
LTE : "kleiner oder gleich"
EQ : "gleich"
NEQ : "ungleich"
ASSIGN : "erhält"
M_COMMENT : {M_COMMENT}
COMMENT : {COMMENT}
SHIFT_RIGHT: ">>"
SHIFT_LEFT: "<<"
BIT_AND: "&"
BIT_OR: "|"
BIT_NOT: "~"
BIT_XOR: "^"
AND : "und"
OR : "oder"
NOT : "nicht"
VOID : "leer" //void
INT : "ganze" //integer
FLOAT : "reelle" //decimal
STRING : "zeichenkette" //string
BOOL : "boolesche" //boolean
CHAR: "zeichen" //char
FUNC : "funktion" //function
RETURN : "zurückliefern" //return
GIVES : "liefert" //gives
BACK : "zurück" //back, as in: gives int back; liefert ganze zurÃ¼ck
IF : "falls" //if
ELSE : "sonst" //else
FALSE : "falsch" //false
TRUE: "wahr" //true
WHILE : "während" //while
FOR : "für" //for
EACH : "jede"
OF : "von"
DO : "tun" //do
READ: "lesen"
WRITE: "schreiben"

ID : {ID}

: [\s\t\n\r]
#NonTerminals
<program>

<body>
<function>

<parameters>
<parameter_rec>
<parameter>

<statements>
<statement_rec>
<statement>

<comment>

<block>
<if>
<if_else>
<for>
<for_each>
<while>
<do_while>

<instruction>

<declaration>
<decl_assign>
<decl_only>

<identifiers>
<id_decl>

<assignment>
<unary_assign>

<return>

<id>

<func_call>
<args>
<args_rec>

<input>
<output>

<exp>
<exp0>
<exp1>
<exp2>
<exp3>
<exp4>
<exp5>
<exp6>
<exp7>
<exp8>
<exp9>
<exp10>

<vector>

<literal>
<relational>
<negation>
<bitwise>
<arith_low>
<arith_high>
<type>
#Grammar
<program>	::= <program> <function> | <function>;

<body>		::= CB_OPEN <statements> CB_CLOSE;
<function>	::= FUNC ID #32 RB_OPEN <parameters> RB_CLOSE GIVES <type> BACK #2000 <body>;

<parameters>	::= <parameter_rec> | î;
<parameter_rec> 	::= <parameter> COMMA <parameter_rec> | <parameter>;
<parameter>	::= <type> <id>;

<statements>	::= <statement_rec> | î;
<statement_rec>	::= <statement> <statement_rec> | <statement>;
<statement>	::= <block> | <comment> | <instruction> END;

<comment>	::= COMMENT | M_COMMENT;

<block>		::= <if> | <if_else> | <while> | <for> | <for_each> | <do_while>;
<if>		::= IF RB_OPEN <exp> RB_CLOSE <body>;
<if_else>	::= <if> ELSE <body>;
<for>		::= FOR RB_OPEN <declaration> END <exp> END <assignment> RB_CLOSE <body>;
<for_each>	::= FOR EACH RB_OPEN <type> <id> OF <exp> RB_CLOSE <body>;
<while>		::= WHILE RB_OPEN <exp> RB_CLOSE <body>;
<do_while>	::= DO <body> WHILE RB_OPEN <exp> RB_CLOSE END;

<instruction>	::= <assignment> | <declaration> | <input> | <output> | <return> | <exp>;

<declaration>	::= <decl_assign> | <decl_only>;
<decl_assign>	::= <type> <id_decl> ASSIGN <exp> #2000 #4;
<decl_only>	::= <type> <identifiers> #2000;

<identifiers>	::= <id_decl> COMMA <identifiers> | <id_decl>;
<id_decl>	::= ID #31 | ID #31 SB_OPEN INT_LIT #50 SB_CLOSE;

<assignment>	::= <id> ASSIGN <exp> #4 | <unary_assign> ;
<unary_assign>	::= <id> INCREMENT | <id> DECREMENT;

<return>	::= RETURN | RETURN <exp>;

<id>		::= ID #30 | ID #30 <vector>;

<func_call>	::= ID RB_OPEN <args> RB_CLOSE;
<args>		::= <args_rec> | î;
<args_rec>	::= <exp> COMMA <args_rec> | <exp>;

<input>		::= READ <id>;
<output>	::= WRITE <exp>;

<exp> 		::= <exp> OR <exp1> | <exp1>;
<exp1>		::= <exp1> AND <exp2> | <exp2>;
<exp2>		::= <exp2> BIT_OR <exp3> | <exp3>;
<exp3>		::= <exp3> BIT_XOR <exp4> #401 | <exp4>;
<exp4>		::= <exp4> BIT_AND <exp5> #402 | <exp5>;
<exp5>		::= <exp5> <relational> <exp6> #403 | <exp6>;
<exp6>		::= <exp6> <bitwise> <exp7> #404 | <exp7>;	
<exp7>		::= <exp7> <arith_low> <exp8> #405 | <exp8>;
<exp8>		::= <exp8> <arith_high> <exp9> #406 | <exp9> ;
<exp9> 		::= <negation> <exp10> | <exp10>;
<exp10>		::= <id> #610 | RB_OPEN <exp> RB_CLOSE | <literal> | <func_call>;

<vector> 	::= SB_OPEN <exp> SB_CLOSE;

<literal>	::= INT_LIT #601 | FLOAT_LIT #602 | BIN_LIT #603 | HEX_LIT #604 | STRING_LIT #605 | TRUE #606 | FALSE #607;
<relational>	::= GT | LT | GTE | LTE | EQ | NEQ; 
<negation>	::= MINUS | BIT_NOT | NOT;
<bitwise>	::= SHIFT_LEFT | SHIFT_RIGHT;
<arith_low> 	::= PLUS #300 | MINUS #300;
<arith_high>	::= MULT #300 | DIV #300 | MOD #300 ;
<type>		::= INT #1 | FLOAT #1 | STRING #1 | BOOL #1 | VOID #1 | CHAR #1;
